plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.77'
    id 'com.diffplug.spotless' version '7.1.0'
    id 'checkstyle'
}

apply from: "./config/gradle/common.gradle"

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

neoForge {
    // Specify the version of NeoForge to use.
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    // This line is optional. Access Transformers are automatically detected
    // accessTransformers.add('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            client()

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'neoforge.enabledGameTestNamespaces', "${project.mod_id},${project.test_mod_id}"
        }

        client2 {
            client()
            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'neoforge.enabledGameTestNamespaces', "${project.mod_id},${project.test_mod_id}"
            programArguments.addAll('--username', 'Dev2')
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', "${project.mod_id},${project.test_mod_id}"
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', "${project.mod_id},${project.test_mod_id}"
        }

        data {
            clientData()

            // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
            // gameDirectory = project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        // applies to all the run configs above
        configureEach {
            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        // define mod <-> source bindings
        // these are used to tell the game which sources are for which mod
        // mostly optional in a single mod project
        // but multi mod projects should define one per mod
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
        "${test_mod_id}" {
            sourceSet(project(":totr").sourceSets.main)
        }
    }

    accessTransformers {
        file('src/main/resources/META-INF/accesstransformer.cfg')
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    implementation "software.bernie.geckolib:geckolib-neoforge-${minecraft_version}:${geckolib_version}"

    implementation "dev.architectury:architectury-neoforge:${architectury_version}"
    implementation "me.shedaniel:RoughlyEnoughItems-neoforge:${rei_version}"
    implementation "me.shedaniel.cloth:cloth-config-neoforge:${cloth_config_version}"

    runtimeOnly project(":totr")

    runtimeOnly "maven.modrinth:accessible-step:IAcdZjft"
    runtimeOnly "maven.modrinth:all-the-wood-weve-got:P63JGxfL"
    runtimeOnly "maven.modrinth:ametrin:fidFn6Hy"
    runtimeOnly "maven.modrinth:block-variants:G93aiil4"
    runtimeOnly "maven.modrinth:chisel-reborn:CoRSKg1F"
    runtimeOnly "maven.modrinth:chunky:D99gXKQD"
    runtimeOnly "maven.modrinth:clumps:2zQSp93Z"
    runtimeOnly "maven.modrinth:cobweb:iK3CBVfU"
    runtimeOnly "maven.modrinth:collective:I5jY2gQ2"
    runtimeOnly "maven.modrinth:controlling:12sYUzqU"
    runtimeOnly "maven.modrinth:corgilib:1KDvAIdJ"
    runtimeOnly "maven.modrinth:crash-assistant:UeEYjm21"
    runtimeOnly "maven.modrinth:cryonicconfig:UkGnQY0h"
    runtimeOnly "maven.modrinth:ferrite-core:gTYCIFFQ"
    runtimeOnly "maven.modrinth:fusion-connected-textures:kVng83QY"
    runtimeOnly "maven.modrinth:geckolib:eQtABRub"
    runtimeOnly "maven.modrinth:glassential-renewed:ZdmKqZ7M"
    runtimeOnly "maven.modrinth:immediatelyfast:bxFcCVNQ"
    runtimeOnly "maven.modrinth:jade:Rpe14jmy"
    runtimeOnly "maven.modrinth:lithostitched:sidG8iZo"
    runtimeOnly "maven.modrinth:modernfix:xbg7UvQT"
    runtimeOnly "maven.modrinth:modonomicon:1ihreVgB"
    runtimeOnly "maven.modrinth:oh-the-biomes-weve-gone:dzPCoFsI"
    runtimeOnly "maven.modrinth:oh-the-trees-youll-grow:5XdOADwm"
    runtimeOnly "maven.modrinth:puzzles-lib:HOC9OUuE"
    runtimeOnly "maven.modrinth:searchables:gYDrOD6p"
    runtimeOnly "maven.modrinth:simple-backups:6JQL4qSR"
    runtimeOnly "maven.modrinth:spark:2E68cgve"
    runtimeOnly "maven.modrinth:stoneworks:lqIPkuUt"
    runtimeOnly "maven.modrinth:supermartijn642s-config-lib:qKL9jM75"
    runtimeOnly "maven.modrinth:supermartijn642s-core-lib:FFaduWoO"
    runtimeOnly "maven.modrinth:xaeros-minimap:zqaoRYUF"
    runtimeOnly "maven.modrinth:xaeros-world-map:OU4FryfL"

    runtimeOnly "maven.modrinth:shut-up-rei:1.0"
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [minecraft_version      : minecraft_version,
                             minecraft_version_range: minecraft_version_range,
                             neo_version            : neo_version,
                             neo_version_range      : neo_version_range,
                             loader_version_range   : loader_version_range,
                             mod_id                 : mod_id,
                             mod_name               : mod_name,
                             mod_license            : mod_license,
                             mod_version            : mod_version,
                             mod_authors            : mod_authors,
                             mod_description        : mod_description,
                             sophisticated_backpacks_version_range : sophisticated_backpacks_version_range,
                             motr_version_range: motr_version_range]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask generateModMetadata

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}
